apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'com.google.gms.google-services'

def isRunningOnTravis = System.getenv("CI") == "true" && file('../hackillinois.keystore').exists() && file('../service-account-key.p12').exists()

if (isRunningOnTravis) {
    apply plugin: 'com.github.triplet.play'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.hackillinois.android"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10
        versionName "1.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            if (isRunningOnTravis) {
                storeFile file("../hackillinois.keystore")
                storePassword System.getenv("keystore_password")
                keyAlias System.getenv("key_alias")
                keyPassword System.getenv("key_password")
            }
        }
    }

    buildTypes {
        def google_maps_api_key = isRunningOnTravis ?
                (System.getenv("GOOGLE_MAPS_API_KEY") ?: "") :
                (project.properties["GOOGLE_MAPS_API_KEY"] ?: "")
        debug {
            resValue "string", "google_maps_api_key", google_maps_api_key
        }
        release {
            applicationIdSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isRunningOnTravis) {
                signingConfig signingConfigs.release
            }
            resValue "string", "google_maps_api_key", google_maps_api_key
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'io.github.luizgrp.sectionedrecyclerviewadapter:sectionedrecyclerviewadapter:3.1.0'
    implementation 'com.airbnb.android:lottie:3.3.1'
    implementation 'androidx.fragment:fragment:1.2.0'

    // Zoomable images
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Firebase notifications
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'

    // Room Database
    implementation 'androidx.room:room-runtime:2.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt 'androidx.room:room-compiler:2.0.0'

    // Refreshable recyclerview
    implementation 'com.dinuscxj:recyclerrefreshlayout:2.0.5'

    // Play Services
    implementation 'com.google.android.gms:play-services-maps:16.0.0'

    // QR Code Scanning + Generation
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation 'com.google.zxing:core:3.3.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

if (isRunningOnTravis) {
    play {
        track = "alpha"
        userFraction = 1
        serviceAccountEmail = "hackillinois-android-publishin@api-7971352790762652858-223664.iam.gserviceaccount.com"
        serviceAccountCredentials = file("../service-account-key.p12")

        resolutionStrategy = "auto"
        outputProcessor { // this: ApkVariantOutput
            versionNameOverride = "$versionNameOverride.$versionCode"
        }
    }
}
